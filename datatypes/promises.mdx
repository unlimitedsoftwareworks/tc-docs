Promise DataType
===

The best way to describe promise, is that they are the same as a regular JavaScript object, except they are built-in data types within the language.

A promise represent a piece of data that will be available in the future. A promise can be awaited, or assigned a callback function to be executed when the data is available, or simply ignored.

```tc
from std.fs import writeAsync
from std.io import println

fn main(args: String[]) {
    let content = "Hello, World!"
    let done: promise<u32> = writeAsync("hello.txt", content)
    done.then(fn(res: u32) {
        if res == 0 {
            println("File written successfully!")
        } else {
            println("Failed to write file! Error code" + res)
        }
    })

    return 0;
}
```

Now in this snippet, it is not an issue that the main process exists while we have an Async operation running, the VM will make sure everything is complete before completely shutting down the VM process. The main issue is that ideally, we would return an error instead of 0 if the write fails. Assigning a callback function is not the way to handle it, since we can't return a value from a callback function back to the main. This is where the `await` keyword comes in.

```tc
from std.fs import writeAsync
from std.io import println

fn main(args: String[]) {
    let content = "Hello, World!"
    let res: u32 = await writeAsync("hello.txt", content)
    if res == 0 {
        println("File written successfully!")
    } else {
        println("Failed to write file! Error code" + res)
    }

    return 0;
}
```

The `await` keyword will pause the main process until the promise is resolved. Awaiting a `promise<T>` will return the value of type `T` that the promise resolves to. Error handeling is left to the user, meaning, there is no `reject` of a promise. If you want to model an error, you can return an error code, or a `variant` type.

```tc
type WriteResponse = variant {
    Ok,
    Error(reason: String)
}
```
## Promises In Type-C
Even though promises might sound very much like JavaScript, there is actually a huge difference on how they are implemented in Type-C.

1. **Promise Creation**: Promises cannot be created by the user, they are created within the VM when certain async APIs are called, or spawning a thread for example.
2. **Resolving Promises**: Promises can only be resolved by the VM, the user cannot resolve a promise.
3. **Rejecting a Promise**: There is no such thing as rejecting a promise in Type-C. A promise will always return a value, that value could be an error code, or a variant type that represents an error, that's up to the user to decide.

## Promise.then usage

The `promise.then` is a method that takes a callback function as an argument. The callback function will be executed when the promise is resolved. The callback function will be passed the resolved value of the promise as an argument.
Unlike JavaScript, the `then` method does not return a new promise, it returns the output of the callback function. Hence it can only be cascaded as long as you are returning a promise from the callback function.

Here is the basic syntax of `.then` method:
```tc
promise<U>.then<V>(fn(res: U) -> V { .. })
promise<U>.then< promise<V> >(
    fn(res: U) -> promise<V> { .. }
).then<W>(
    fn(res: V) -> W { .. }
)
```

A very basic example showcasing this as follows:
```tc
fn f1() -> i32 {
    let z: i32 = 1
    return z
}

fn f2() -> u64 {
    let z: u64 = 1
    return z
}

let thread1 = spawn f1()

thread1.then(fn(x: i32) {
    return spawn f2()
}).then(fn(x: u64)  {
    return spawn f1()
}).then<void>(fn(x: i32) {
    return
})

let l: lock<u32> = new lock(0)
l.withLock( fn(x: u32) {
    return spawn f1()  
}).then(fn(x: i32) {
    return
})
```

It is recommended to not specify the type argument for the `.then` method, as the compiler can infer the type from the callback function.

### .then Callback argument mutability
The callback function passed to the `.then` method cannot have a mutable argument. 

Meaning 
```tc
thread1.then< i32, promise<u64> >(fn(mut x: i32) -> promise<u64> {
    return spawn f2()
})
```

Is wrong and will not compile.
