Classes is Type-C serve to define objects with hidden state and behavior. Classes are the building blocks of object-oriented programming,
and they allow for the creation of complex data structures and systems. Classes in Type-C are defined using the `class` keyword,
followed by the class name and a block of code that contains the class attributes and methods.

## Basic Example:

```tc
type Person = class {
    let name: string
    let age: u32

    fn init(name: string, age: u32) {
        this.name = name
        this.age = age
    }

    fn sayHello() {
        println("Hello, my name is {} and I am {} years old.", name, age)
    }
}
```

## Encapsluation
Type-C supports encapsulation through the `local` keyword, indicating that an attribute or method is only accessible within the class.

```tc
type Person = class {
    let local const name: string
    let local age: u32

    fn init(name: string, age: u32) {
        this.name = name
        this.age = age
    }

    fn sayHello() {
        println("Hello, my name is {} and I am {} years old.", this.name, this.age)
    }

    fn setAge(age: u32) {
        if age < 120 {
            this.age = age
        }
    }
}
```
## Constant State
When class attributes are declared as `const`, they can only be modified within a constructor (i,e `init` method).
If an attribute is modified outside the constructor, the compiler will raise an error.

In other words, the `init` function will ignore all `const` checks for write access on the attributes of the class
being initialized.

## Implementing Interfaces
Classes in Type-C can implement interfaces, increasing their functionality and making them more versatile.

```tc
type Serializable = interface  {
    fn serialize() -> u8[]
}

type Deserializable<T> = interface  {
    fn deserialize(data: u8[]) -> T
}

type Person = class Serializable, Deserializable<Person> {
    let name: String
    let age: u32

    fn init(name: String, age: u32) {
        this.name = name
        this.age = age
    }

    fn serialize() -> u8[] {
        // Serialization logic here
    }

    fn deserialize(data: u8[]) -> Person {
        // Deserialization logic here
    }
}
```

## Classes and Method Overloading
Method overloading is allowed within classes and interfaces. A method signature is what identifies a method, and it is composed of the method name
and the types of its parameters. Hence, two methods with the same name but different parameter types are considered overloaded.
Return type within the signature is not considered, hence if two methods have the same name and parameter types but different return types,
they are considered duplicates and will cause a compilation error.

```tc
type Array<T> = class {
    let data: T[]

    fn init(data: T[]) {
        this.data = data
    }

    fn init() {
        this.data = []
    }

    // overload the index [] operator
    fn [](idx: u64) = this.data[idx]
}
```
You can overload an operator by using its symbol or name (surrounded with two underscores) such as `+` or `__add__`
It is possible to overload the following operators:
```ts
{
    "+": "__add__",
    "-": "__sub__",
    "*": "__mul__",
    "/": "__div__",
    "%": "__mod__",
    "<": "__lt__",
    ">": "__gt__",
    "<=": "__le__",
    ">=": "__ge__",
    ">>": "__rshift__",
    "<<": "__lshift__",
    "&": "__band__",
    "|": "__bor__",
    "^": "__xor__",
    "&&": "__and__",
    "||": "__or__",
    "!": "__not__",
    "~": "__bnot__",
    "[]": "__index__",
    "[-]": "__reverse_index__",
    "[]=": "__index_set__",
    "[-]=": "__reverse_index_set__",
    "()": "__call__",
    "++": "__inc__",
    "--": "__dec__"
}
```

## Declaring same Method with Multiple Names
Consider the case where an interface has the following signature:
```tc
type Indexable<T> = interface {
    fn get(idx: u64) -> T
}
```

You now want to have a class that implements this interface, but you want to also use `[]` to achieve same behavior as `get`.

```tc
type IndexableClass<T> = class Indexable<T> {
    fn get | [] (idx: u64) -> T {..}
}
```

separator `|` is used to separate the different names of the method.


## Classes and Inheritance
It is better to think of classes as concrete behaviors rather than a hierarchy of types.
Classes implement interface, which are merely protocols for behavior. This is a subtle but important distinction.

## Rules
- Class names must start with a capital letter.
- Classes support generics
- Classes can implement multiple interfaces
- Classes can have multiple constructors
- Classes can have static methods
- Classes can have static attributes
- Classes can have implementations
- Classes' static methods cannot use the class generic type
- Classes' attributes cannot be generic
- Classes can extend generic interfaces.
- Classes constructor must return void, they are only meant to initialize the state of the object.

## Advanced Topics

Suppose we have the following data types:

```tc
type X<T> = class {
    fn get(x: T) -> T {
        return x
    }
}

let z: interface {fn get(x: u32) -> u32} = new X()
```

This is completely valid, as long as the interface header is compatible with the generic class constraints. Here is a case where is it is not possible:

```tc
type X = class {
    fn get<T: Indexable>(x: T) -> T {
        return x
    }
}

let z: interface {fn get(x: u32) -> u32} = new X() // Woupsy
```

Will fail since the given type argument does not satisfy the constraints of the generic class.

Also generics that have multiple constraints, will need to have only one when made concrete, otherwise compiler will fail, here is another example:

```tc
fn dostuff<T: Circle | Cube>(x: T, y: T)

let circle: Circle = new Circle()
let cube: Cube = new Cube()

dostuff(circle, cube) // FAIL
```
